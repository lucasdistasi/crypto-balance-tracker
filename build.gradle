plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.0'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'jacoco'
}

group = 'com.distasilucas'
version = '1.0.0-SNAPSHOT'

bootJar {
	archiveFileName = "${project.name}.jar"
	launchScript()
}

ext {
	ehcacheVersion = "3.10.8"
	jacocoVersion = "0.8.9"
	jacocoExecPath = "${project.rootDir}/../target/jacoco.exec"
	javaxCacheVersion = "1.1.1"
	lombokVersion = "1.18.30"
	okHttp3Version = "4.12.0"
	springDocVersion = "2.2.0"
	springRetryVersion = "2.0.4"
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
}

jacoco {
	toolVersion = jacocoVersion
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-actuator"
	implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
	implementation "org.springframework.boot:spring-boot-starter-validation"
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "org.springframework.boot:spring-boot-starter-cache"
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"
	implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
	implementation "org.springframework.retry:spring-retry:$springRetryVersion"
	implementation "com.squareup.okhttp3:okhttp:$okHttp3Version"
	implementation "org.ehcache:ehcache:$ehcacheVersion"
	implementation "javax.cache:cache-api:$javaxCacheVersion"

	compileOnly "org.projectlombok:lombok:${lombokVersion}"

	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

	testImplementation "com.squareup.okhttp3:mockwebserver:$okHttp3Version"
	testImplementation "org.springframework.boot:spring-boot-starter-test"
}

tasks.named('bootBuildImage') {
	builder = 'paketobuildpacks/builder-jammy-base:latest'
}

tasks.named('test') {
	useJUnitPlatform()
}

jacocoTestReport {
	dependsOn test

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					"**/CryptoBalanceTrackerApplication**",
					"**/configuration/**"
			])
		}))
	}

	reports {
		csv.required.set(true)
		html.required.set(true)
	}
}

test {
	finalizedBy jacocoTestReport
}